plugins {
    id 'com.android.application'
    id 'kotlin-android'
    alias(libs.plugins.gradle.android.cachefix)
}

android {
    compileSdkVersion 33
    defaultConfig {
        applicationId "org.alberto97.aircontroller"
        namespace "org.alberto97.aircontroller"
        minSdkVersion 23
        targetSdkVersion 33
        versionCode 2
        versionName "1.0.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        def localProps = new Properties()
        if (file("local.properties").exists()) {
            file("local.properties")?.withInputStream { localProps.load(it) }
        }

        def defaultDemoAccount = "{\"demo@example.com\"}"
        buildConfigField "String[]", "DEMO_ACCOUNTS", (localProps.getProperty("DEMO_ACCOUNTS") ?: defaultDemoAccount)
    }

    buildFeatures {
        compose true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    composeOptions {
        kotlinCompilerExtensionVersion "1.4.3"
    }

    kotlinOptions {
        jvmTarget = "11"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])

    implementation project(":provider-base")
    implementation project(":provider-ayla")
    implementation project(":provider-demo")

    // AndroidX Compose
    def composeBom = platform(libs.androidx.compose.bom)
    implementation(composeBom)
    androidTestImplementation(composeBom)
    implementation libs.androidx.compose.material
    implementation libs.androidx.compose.materialIconsExt
    implementation libs.androidx.activity.activityCompose
    implementation libs.androidx.compose.ui.tooling.preview
    debugImplementation libs.androidx.compose.ui.tooling

    // AndroidX Core Splashscreen
    implementation libs.androidx.core.splashscreen

    // AndroidX Data Store
    implementation libs.androidx.datastore.preferences

    // AndroidX Navigation
    implementation libs.androidx.navigation.compose

    // Koin
    implementation libs.koin.android
    implementation libs.koin.androidxCompose

    testImplementation libs.junit
}
